plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

javafx {
    version = "13"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

sourceCompatibility = 12
group 'com.mythicalcreaturesoftware'
version '0.1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://dl.bintray.com/jerady/maven"
    }
    maven {
        url "https://jitpack.io"
    }
}

application {
    mainClassName = 'com.mythicalcreaturesoftware.splash.Main'
}

dependencies {
    implementation 'de.saxsys:mvvmfx:1.8.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.11.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.controlsfx:controlsfx:11.0.0'
    implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'
    implementation 'de.jensd:fontawesomefx-materialdesignfont:2.0.26-9.1.2'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-9.1.2'
    implementation 'de.jensd:fontawesomefx-materialstackicons:2.1-5-9.1.2'
    implementation 'com.github.goxr3plus:FX-BorderlessScene:4.3.1'
    implementation 'com.mythicalcreaturesoftware:reader-jvm:0.0.1'

    annotationProcessor 'de.saxsys:mvvmfx:1.8.0'

    testImplementation 'junit:junit:4.12'
}

task generateExec(type: Exec) {
    dependsOn build

    workingDir "$projectDir/build"

    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'jPackage ' +
                ' --input libs ' +
                '--main-jar app-' + version + '-all.jar ' +
                '--name SplashComicReader ' +
                '--description "Application to read .cbr and .cbz files" ' +
                '--vendor "Mythical Creature Software" ' +
                '--copyright "Copyright 2020, All rights reserved" ' +
                '--icon ../generalResources/icon.ico ' +
                '--win-shortcut ' +
                '--win-dir-chooser ' +
                '--win-menu ' +
                '--win-menu-group "Mythical Creature Software"'
                '--app-version ' + version + ' '
    } else if ((System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('linux'))) {
        def jPackage = System.getenv('JAVA_HOME') + "/bin/jpackage"
        commandLine 'sh', '-c', jPackage +
                ' --input libs ' +
                '--main-jar app-' + version + '-all.jar ' +
                '--name SplashComicReader ' +
                '--linux-package-name splash-comic-reader ' +
                '--description "Application to read .cbr and .cbz files" ' +
                '--vendor "Mythical Creature Software" ' +
                '--copyright "Copyright 2020, All rights reserved" ' +
                '--icon ../generalResources/icon.png ' +
                '--file-associations ../generalResources/cbrFile.properties ' +
                '--file-associations ../generalResources/cbzFile.properties ' +
                '--app-version '  + version
    } else if ((System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('mac'))) {
        def jPackage = '/Library/Java/JavaVirtualMachines/adoptopenjdk-14.jdk/Contents/Home' + "/bin/jpackage"
        commandLine 'sh', '-c', jPackage +
                ' --input libs ' +
                '--main-jar app-' + version + '-all.jar ' +
                '--name SplashComicReader ' +
                '--mac-package-name splash-comic-reader ' +
                '--description "Application to read .cbr and .cbz files" ' +
                '--vendor "Mythical Creature Software" ' +
                '--copyright "Copyright 2020, All rights reserved" ' +
                '--icon ../generalResources/icon.icns ' +
                '--app-version '  + version
    }
}